#version 460 core

layout(local_size_x=64) in;

struct Particle {
    vec2 position;
    float angleDirection;
    float PADDING;
};

layout(binding = 0, std430)buffer Particles{ Particle particles[]; };
layout(r32f, binding = 1) uniform image2DArray texTrail;

#define PI 3.1415
#define TRUE 1
#define FALSE 0

uniform float senseAngle;
uniform float senseDistance;
uniform float turnSpeed;
uniform int particleCount;
uniform float movementSpeed;
uniform float deltaT;
uniform float time;
uniform float trailWeight;
uniform float maxTrailValue;
uniform int sensorSize;

uniform int enableAttractor;
uniform vec2 attractorPosition;

vec2 rotate(vec2 v, float a) {
    float s = sin(a);
    float c = cos(a);
    mat2 m = mat2(c, -s, s, c);
    return m * v;
}

// select most pheromone in 3 dirs
// set direction toward that - limited by turnAngle
// move in direction
// save pheromone to trail

struct SenseResult {
    float trail;
    vec2 dir;
    float angle;
};

uint hash(uint state) {
    state ^= 2747636419u;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    state ^= state >> 16;
    state *= 2654435769u;
    return state;
}

float scaleToRange01(uint state) {
    return float(state) / 4294967295.0;
}

float sense(Particle particle, float sensorAngle, ivec2 texSize) {
    const uint speciesId = gl_GlobalInvocationID.z;
    const float angle = particle.angleDirection + sensorAngle;
    const vec2 sensorDir = vec2(cos(angle), sin(angle));

    const vec2 sensorPos = particle.position + sensorDir * senseDistance;
    const ivec2 iSensorPos = ivec2(sensorPos);

    const int halfSensorSize = sensorSize / 2;
    const ivec2 texSensorPos = ivec2(min(texSize.x - 1, max(0, iSensorPos.x)), min(texSize.y - 1, max(0, iSensorPos.y)));

    float tmp = 0;
    for (int x = -halfSensorSize; x <= halfSensorSize; ++x) {
        for (int y = -halfSensorSize; y <= halfSensorSize; ++y) {
            tmp += imageLoad(texTrail, ivec3(texSensorPos + ivec2(x, y), speciesId)).r;
        }
    }

    return tmp;
}

vec2 attractorDelta(Particle particle) {
    const float ATT_DIST = 250;
    if (enableAttractor == FALSE) { return vec2(0, 0); }
    vec2 dir = attractorPosition - particle.position;
    const float dist = length(dir);
    if (dist < ATT_DIST) {
        dir = normalize(dir);
        return (1 - dist / ATT_DIST) * dir;
    }
    return vec2(0, 0);
}

void main() {
    if (gl_GlobalInvocationID.x >= particleCount) { return; }

    const uint speciesId = gl_GlobalInvocationID.z;

    Particle particle = particles[gl_GlobalInvocationID.x];
    const vec2 oldPos = particle.position;


    const ivec2 texSize = imageSize(texTrail).xy;
    uint random = hash(uint(oldPos.y * texSize.x + oldPos.x + hash(uint(gl_GlobalInvocationID.x + time * 100000.f))));

    const float angleR = senseAngle * (PI / 180);
    const float fwdVal = sense(particle, 0, texSize);
    const float leftVal = sense(particle, angleR, texSize);
    const float rightVal = sense(particle, -angleR, texSize);

    const float rndSteer = scaleToRange01(random);
    const float turnSpeedR = turnSpeed * 2 * PI;

    if (fwdVal > leftVal && fwdVal > rightVal) {
        //
    } else if (fwdVal < leftVal && fwdVal < rightVal) {
        particles[gl_GlobalInvocationID.x].angleDirection += (rndSteer - 0.5) * 2 * turnSpeedR * deltaT;
    } else if (rightVal > leftVal) {
        particles[gl_GlobalInvocationID.x].angleDirection -= rndSteer * turnSpeedR * deltaT;
    } else if (leftVal > rightVal) {
        particles[gl_GlobalInvocationID.x].angleDirection += rndSteer * turnSpeedR * deltaT;
    }

    const vec2 newDir = vec2(cos(particle.angleDirection), sin(particle.angleDirection));
    vec2 newPos = particle.position + newDir * deltaT * movementSpeed;

    if (newPos.x < 0 || newPos.x >= texSize.x || newPos.y < 0 || newPos.y >= texSize.y) {
        random = hash(random);
        const float randomAngle = scaleToRange01(random) * 2 * PI;
        newPos.x = min(texSize.x - 1, max(0, newPos.x));
        newPos.y = min(texSize.y - 1, max(0, newPos.y));
        particles[gl_GlobalInvocationID.x].angleDirection = randomAngle;
    } else {
        const ivec2 tCoord = ivec2(newPos);
        const float prevTrail = imageLoad(texTrail, ivec3(tCoord, speciesId)).r;
        imageStore(texTrail, ivec3(tCoord, speciesId), vec4(min(maxTrailValue, prevTrail + trailWeight * deltaT)));
    }
    const vec2 attractionDelta = attractorDelta(particle);
    particles[gl_GlobalInvocationID.x].position = newPos + attractionDelta;
}





























